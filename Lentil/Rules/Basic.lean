import Lentil.Basic
import Lentil.Tactics.Basic
import Lentil.Gadgets.TheoremLifting

/-! Basic theorems about TLA. -/

open Classical LentilLib

-- lift a bunch of proplemmas first

-- `And`
#tla_lift and_self_iff and_not_self_iff not_and_self_iff and_comm and_assoc
#tla_lift And.left => TLA.and_left
#tla_lift And.right => TLA.and_right
#tla_lift And.imp_left => TLA.and_imp_left
#tla_lift And.imp_right => TLA.and_imp_right
#tla_lift And.imp => TLA.and_imp_both
#tla_lift not_and_of_not_left not_and_of_not_right and_left_comm and_right_comm
  and_rotate and_and_and_comm and_and_left and_and_right

-- `Or`
#tla_lift or_self_iff or_left_comm or_right_comm or_or_or_comm or_or_distrib_left
  or_or_distrib_right or_rotate or_comm or_assoc
#tla_lift Or.inl => TLA.or_inl
#tla_lift Or.inr => TLA.or_inr

-- distributive laws
#tla_lift and_or_left or_and_right or_and_left and_or_right

-- `Not`
#tla_lift not_and' not_or imp_false not_true
#tla_lift not_false_iff => TLA.not_false

-- `âˆ€`, `âˆƒ`
#tla_lift not_exists forall_and exists_or exists_and_left exists_and_right
  forall_comm exists_comm
#tla_lift Bool.forall_bool Bool.exists_bool'

-- `Decidable`, but we are in the classical setting
#tla_lift Decidable.not_not Decidable.by_contra Decidable.not_imp_comm
  Decidable.not_imp_self Decidable.or_iff_not_imp_left
  Decidable.imp_iff_or_not Decidable.not_and_not_right
  Decidable.or_iff_not_not_and_not Decidable.and_iff_not_not_or_not
#tla_lift Decidable.not_and_iff_not_or_not => TLA.not_and
#tla_lift Decidable.imp_iff_not_or => TLA.implies_to_or
#tla_lift Decidable.not_imp_iff_and_not => TLA.not_implies_to_and
#tla_lift Decidable.not_imp_not => TLA.contraposition_for_tla_implies

namespace TLA

section playground

variable {Ïƒ : Type u}

theorem dual_lemma (p q : pred Ïƒ) : Â¬ p =tla= Â¬ q â†’ (p) =tla= (q) := by
  unfold tla_not ; intro h ; funext e ; have := congrFun h e ; aesop

theorem pred_eq_iff_iff (p q : pred Ïƒ) : (p) =tla= (q) â†” (p) |-tla- (q) âˆ§ (q) |-tla- (p) := by
  tla_unfold_simp ; constructor
  Â· aesop
  Â· intro h ; funext e ; aesop

section structural

theorem impl_intro (p q : pred Ïƒ) : |-tla- (p â†’ q) = (p) |-tla- (q) := rfl

theorem impl_decouple (p q : pred Ïƒ) : |-tla- (p â†’ q) â†’ |-tla- (p) â†’ |-tla- (q) := by
  tla_unfold_simp ; aesop

theorem and_pred_implies_split (Î“ p q : pred Ïƒ) : (Î“) |-tla- (p âˆ§ q) = ((Î“) |-tla- (p) âˆ§ (Î“) |-tla- (q)) := by
  tla_unfold_simp ; aesop

theorem and_valid_split (p q : pred Ïƒ) : |-tla- (p âˆ§ q) = (|-tla- (p) âˆ§ |-tla- (q)) := by
  tla_unfold_simp ; aesop

theorem impl_intro_add_r (p q r : pred Ïƒ) : (r) |-tla- (p â†’ q) = (r âˆ§ p) |-tla- (q) := by
  tla_unfold_simp

theorem impl_drop_hyp (p q : pred Ïƒ) : |-tla- (q) â†’ (p) |-tla- (q) := by
  tla_unfold_simp ; aesop

theorem impl_drop_hyp_one_r (p q r : pred Ïƒ) : (p) |-tla- (q) â†’ (p âˆ§ r) |-tla- (q) := by
  tla_unfold_simp ; aesop

end structural

section one

variable (p : pred Ïƒ)

theorem always_intro : (|-tla- (p)) = (|-tla- (â–¡ p)) := by
  tla_unfold_simp ; constructor
  Â· aesop
  Â· intro h ; exact (fun e => h e 0)

theorem later_always_comm : (â—¯ â–¡ p) =tla= (â–¡ â—¯ p) := by
  funext e ; tla_unfold_simp
  constructor <;> intro h k <;> rw [Nat.add_comm] <;> apply h

theorem always_unroll : â–¡ p =tla= (p âˆ§ â—¯ â–¡ p) := by
  rw [later_always_comm]
  funext e ; tla_unfold_simp
  constructor
  Â· intro h ; apply And.intro (h 0) (by aesop)
  Â· intro âŸ¨h0, hsâŸ© k ; cases k with
    | zero => exact h0
    | succ k => apply hs

theorem always_induction : â–¡ p =tla= (p âˆ§ â–¡ (p â†’ â—¯ p)) := by
  funext e ; tla_unfold_simp
  constructor
  Â· intro h ; apply And.intro (h 0) (by aesop)
  Â· intro âŸ¨h0, hsâŸ© k ; induction k <;> aesop

theorem always_weaken : â–¡ p |-tla- (p) := by
  tla_unfold_simp ; intro e h ; apply h 0

theorem always_weaken_to_eventually : â–¡ p |-tla- â—‡ p := by
  tla_unfold_simp ; intro e h ; exists 0 ; apply h

theorem later_weaken_to_eventually : â—¯ p |-tla- â—‡ p := by
  tla_unfold_simp ; intro e h ; exists 1

theorem now_weaken_to_eventually : (p) |-tla- â—‡ p := by
  tla_unfold_simp ; intro e h ; exists 0

theorem not_always : (Â¬ â–¡ p) =tla= (â—‡ Â¬ p) := by
  funext e ; tla_unfold_simp

@[tladual]
theorem not_eventually : (Â¬ â—‡ p) =tla= â–¡ Â¬ p := by
  funext e ; tla_unfold_simp

theorem eventually_to_always : â—‡ p =tla= Â¬ â–¡ Â¬ p := by
  funext e ; tla_unfold_simp

@[tladual]
theorem always_to_eventually : â–¡ p =tla= Â¬ â—‡ Â¬ p := by
  funext e ; tla_unfold_simp

theorem always_idem : (â–¡ â–¡ p) =tla= â–¡ p := by
  funext e ; tla_unfold_simp
  constructor <;> intro h
  Â· intro k ; apply h _ 0
  Â· intros ; apply h

@[tladual]
theorem eventually_idem : (â—‡ â—‡ p) =tla= â—‡ p := by
  apply dual_lemma ; simp [not_eventually, always_idem]

theorem always_eventually_always : (â–¡ â—‡ â–¡ p) =tla= â—‡ â–¡ p := by
  funext e ; ext ; constructor
  on_goal 1=> apply always_weaken
  tla_unfold_simp ; intro kk h k ; exists kk ; intros k2
  have hq : k + kk + k2 = kk + (k + k2) := by omega
  rw [hq] ; apply h

@[tladual]
theorem eventually_always_eventually : (â—‡ â–¡ â—‡ p) =tla= â–¡ â—‡ p := by
  apply dual_lemma ; simp [not_eventually, not_always, always_eventually_always]

theorem always_eventually_idem : (â–¡ â—‡ â–¡ â—‡ p) =tla= â–¡ â—‡ p := by
  simp [eventually_always_eventually, always_idem]

@[tladual]
theorem eventually_always_idem : (â—‡ â–¡ â—‡ â–¡ p) =tla= â—‡ â–¡ p := by
  simp [always_eventually_always, eventually_idem]

end one

attribute [tlasimp] not_not not_always not_eventually always_idem eventually_idem
  always_eventually_always eventually_always_eventually
  always_eventually_idem eventually_always_idem

section two

variable (p q : pred Ïƒ)

theorem contraposition_for_pred_implies : (p) |-tla- (q) = ((Â¬ q) |-tla- Â¬ p) := by
  repeat rw [â† impl_intro, â† contraposition_for_tla_implies]

theorem proof_by_contra : (p) |-tla- (q) = (Â¬ q âˆ§ p) |-tla- (âŠ¥) := by
  rw [contraposition_for_pred_implies] ; tla_unfold_simp

theorem modus_ponens : (p âˆ§ (p â†’ q)) |-tla- (q) := by
  tla_unfold_simp ; aesop

theorem modus_ponens_with_premise : (p âˆ§ (p â†’ q)) |-tla- (p âˆ§ q) := by
  tla_unfold_simp ; aesop

-- the following: about modal operators

theorem always_and_eventually : (â—‡ p âˆ§ â–¡ q) |-tla- (â—‡ (p âˆ§ q)) := by
  tla_unfold_simp ; aesop

theorem always_and_eventually' : (â—‡ p âˆ§ â–¡ q) |-tla- (â—‡ (p âˆ§ â–¡ q)) := by
  tla_unfold_simp ; aesop

theorem later_monotone : (p) |-tla- (q) â†’ â—¯ p |-tla- â—¯ q := by
  tla_unfold_simp ; aesop

theorem always_monotone : (p) |-tla- (q) â†’ â–¡ p |-tla- â–¡ q := by
  tla_unfold_simp ; aesop

theorem eventually_monotone : (p) |-tla- (q) â†’ â—‡ p |-tla- â—‡ q := by
  tla_unfold_simp ; aesop

theorem always_eventually_monotone : (p) |-tla- (q) â†’ (â–¡ â—‡ p) |-tla- (â–¡ â—‡ q) := by
  intro h ; apply always_monotone ; apply eventually_monotone ; assumption

theorem eventually_always_monotone : (p) |-tla- (q) â†’ (â—‡ â–¡ p) |-tla- (â—‡ â–¡ q) := by
  intro h ; apply eventually_monotone ; apply always_monotone ; assumption

theorem until_induction : (p âˆ§ â–¡ (p âˆ§ Â¬ q â†’ â—¯ p)) |-tla- ((â–¡ (p âˆ§ Â¬ q)) âˆ¨ (p ğ‘ˆ (p âˆ§ q))) := by
  tla_unfold_simp ; intro e hp h
  by_cases h' : (âˆƒ n, q <| e.drop n)
  Â· rcases h' with âŸ¨n', h'âŸ©
    have âŸ¨n', _, hq, hminâŸ© := Nat.find_min (p := fun n_ => q (exec.drop n_ e)) _ h'
    right ; exists n'
    suffices hthis : q (exec.drop n' e) âˆ§ âˆ€ (j : Nat), j â‰¤ n' â†’ p (exec.drop j e) by
      rcases hthis with âŸ¨h1, h2âŸ©
      apply And.intro (And.intro (by apply h2 n' (by simp)) h1) (fun j hlt => h2 _ (by omega))
    apply And.intro hq ; intro j hlt
    induction j with
    | zero => exact hp
    | succ j ih => apply h ; apply ih ; omega ; apply hmin ; omega
  Â· simp at h'
    left ; intro j ; apply And.intro _ (h' _)
    induction j <;> solve_by_elim

end two

section two_with_quantifiers

variable {Î± : Sort v} (p : Î± â†’ pred Ïƒ)

theorem later_forall : (â—¯ âˆ€ x, (p x)) =tla= (âˆ€ x, â—¯ (p x)) := by
  funext e ; tla_unfold_simp

theorem later_exists : (â—¯ âˆƒ x, (p x)) =tla= (âˆƒ x, â—¯ (p x)) := by
  funext e ; tla_unfold_simp

theorem always_forall : (â–¡ âˆ€ x, (p x)) =tla= (âˆ€ x, â–¡ (p x)) := by
  funext e ; tla_unfold_simp ; aesop

theorem eventually_exists : (â—‡ âˆƒ x, (p x)) =tla= (âˆƒ x, â—‡ (p x)) := by
  funext e ; tla_unfold_simp ; aesop

/-- uni-direction, moving `âˆƒ` into `â–¡` -/
theorem exists_into_always : (âˆƒ x, â–¡ (p x)) |-tla- â–¡ (âˆƒ x, (p x)) := by
  tla_unfold_simp ; aesop

/-- uni-direction, moving `â—‡` into `âˆ€` -/
@[tladual]
theorem eventually_into_forall : (â—‡ âˆ€ x, (p x)) |-tla- (âˆ€ x, â—‡ (p x)) := by
  tla_unfold_simp ; aesop

end two_with_quantifiers

section two'

variable (p q : pred Ïƒ)

/- NOTE: in principle we can derive the following via metaprogramming,
   but these proofs are so short, so why bother ... -/
theorem later_and : (â—¯ (p âˆ§ q)) =tla= (â—¯ p âˆ§ â—¯ q) := by
  funext e ; tla_unfold_simp

theorem later_or : (â—¯ (p âˆ¨ q)) =tla= (â—¯ p âˆ¨ â—¯ q) := by
  funext e ; tla_unfold_simp

theorem always_and : (â–¡ (p âˆ§ q)) =tla= (â–¡ p âˆ§ â–¡ q) := by
  funext e ; tla_unfold_simp ; aesop

@[tladual]
theorem eventually_or : (â—‡ (p âˆ¨ q)) =tla= (â—‡ p âˆ¨ â—‡ q) := by
  funext e ; tla_unfold_simp ; aesop

/-- uni-direction, merging the `âˆ¨` outside `â—‡` in -/
theorem always_or_merge : (â–¡ p âˆ¨ â–¡ q) |-tla- â–¡ (p âˆ¨ q) := by
  tla_unfold_simp ; aesop

/-- uni-direction, splitting the `âˆ§` inside `â—‡` -/
@[tladual]
theorem eventually_and_split : (â—‡ (p âˆ§ q)) |-tla- (â—‡ p âˆ§ â—‡ q) := by
  tla_unfold_simp ; aesop

-- NOTE: this __DOES NOT__ apply if we change `âˆ§` into `âˆ€`, unless, e.g. `Î±` is finite!
theorem eventually_always_and_distrib : (â—‡ â–¡ (p âˆ§ q)) =tla= (â—‡ â–¡ p âˆ§ â—‡ â–¡ q) := by
  rw [pred_eq_iff_iff] ; constructor
  on_goal 1=> rw [always_and] ; apply eventually_and_split
  tla_unfold_simp ; intro e n1 h1 n2 h2 ; exists (n1 + n2)
  intro k
  specialize h1 (n2 + k) ; specialize h2 (n1 + k)
  have hq1 : n1 + (n2 + k) = n1 + n2 + k := by omega
  have hq2 : n2 + (n1 + k) = n1 + n2 + k := by omega
  rw [hq1] at h1 ; rw [hq2] at h2 ; aesop

@[tladual]
theorem always_eventually_or_distrib : (â–¡ â—‡ (p âˆ¨ q)) =tla= (â–¡ â—‡ p âˆ¨ â–¡ â—‡ q) := by
  apply dual_lemma ; simp [tlasimp, not_or, eventually_always_and_distrib]

end two'

end playground

end TLA
